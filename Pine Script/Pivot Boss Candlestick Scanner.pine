//@version=4
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© pranjalchaubey

//Structural Pivots
study(title="[CP]Pivot Boss Candlestick Scanner", shorttitle="[CP]Pivot Boss Candlesticks", overlay=true, max_labels_count=500)


// General User Inputs 
//==============================================================================
// Set the date range
startDate   = input(title="Start Date",     type=input.integer, defval=1,       minval=1,       maxval=31,      group="General User Inputs")
startMonth  = input(title="Start Month",    type=input.integer, defval=1,       minval=1,       maxval=12,      group="General User Inputs")
startYear   = input(title="Start Year",     type=input.integer, defval=2010,    minval=1970,    maxval=2100,    group="General User Inputs")

endDate     = input(title="End Date",       type=input.integer, defval=31,      minval=1,       maxval=31,      group="General User Inputs")
endMonth    = input(title="End Month",      type=input.integer, defval=12,      minval=1,       maxval=12,      group="General User Inputs")
endYear     = input(title="End Year",       type=input.integer, defval=2012,    minval=1970,    maxval=2100,    group="General User Inputs")
// Look if the close time of the current bar falls inside the date range
inDateRange = (time >= timestamp(syminfo.timezone, startYear, startMonth, startDate, 0, 0)) and (time < timestamp(syminfo.timezone, endYear, endMonth, endDate, 0, 0))
date_range  = input(false,  title="Use the date range above to mark the Candlestick Setups?",                   group="General User Inputs")


// WICK REVERSAL SETUP 
//==============================================================================
// Inputs
enable_wick_reversal_setup  = input(true,           type=input.bool,                                    title="Enable Wick Reversal Setup",             group="Wick Reversal Setup")
wick_reversal_shape         = input("Circle",       options=["Circle", "Triangle", "Cross", "Diamond"], title="Wick Reversal Marker",                   group="Wick Reversal Setup")
bullish_wick_marker_color   = input(color.green,    type = input.color,                                 title="Bullish Wick Reversal Marker Color",     group="Wick Reversal Setup")
bearish_wick_marker_color   = input(color.red,      type = input.color,                                 title="Bearish Wick Reversal Marker Color",     group="Wick Reversal Setup")
wick2bodyRatio              = input(2.5,            type=input.float,   minval=1, maxval=5,             title="Wick to Body Ratio" ,                    group="Wick Reversal Setup")
closePercentage             = input(35,             type=input.integer, minval=1, maxval=99,            title="Top and Bottom Bar Close Percentage" ,   group="Wick Reversal Setup")


// Wick Reversal Setup Markers 
bullish_wick_reversal_marker = label.style_circle
bearish_wick_reversal_marker = label.style_circle
if wick_reversal_shape == "Triangle"
    bullish_wick_reversal_marker := label.style_triangleup
    bearish_wick_reversal_marker := label.style_triangledown
else if wick_reversal_shape == "Cross"
    bullish_wick_reversal_marker := label.style_xcross
    bearish_wick_reversal_marker := label.style_xcross
else if wick_reversal_shape == "Diamond"
    bullish_wick_reversal_marker := label.style_diamond
    bearish_wick_reversal_marker := label.style_diamond


// Calculations 
actual_close_percentage = closePercentage / 100 
bar_body = abs(open - close)
wick_length = (open - close) <= 0 ? (high - close) + (open - low) : (high - open) + (close - low)
actual_wick_to_body_ratio = wick_length / bar_body
bar_range = abs(high - low)
bull_close_percentage = (high - close)/bar_range
bear_close_percentage = 1 - bull_close_percentage
close_bullish = actual_wick_to_body_ratio >= wick2bodyRatio and bull_close_percentage <= actual_close_percentage ? true : false 
close_bearish = actual_wick_to_body_ratio >= wick2bodyRatio and bear_close_percentage <= actual_close_percentage ? true : false 


// Plots
if not(date_range == true and inDateRange == false)     // Plot only if the bar is in date range, or the feature has been turned off 
    if enable_wick_reversal_setup == true               // Plot only if the user wants to plot the Wick Reversal Setups on the chart 
        if close_bullish == true                        // A bullish wick reversal was found 
            label.new(x=bar_index, y=low, yloc=yloc.belowbar, color=bullish_wick_marker_color, style=bullish_wick_reversal_marker, size=size.auto, tooltip="Bullish Wick Reversal Setup")
        else if close_bearish == true                        // A bearish wick reversal was found 
            label.new(x=bar_index, y=high, yloc=yloc.abovebar, color=bearish_wick_marker_color, style=bearish_wick_reversal_marker, size=size.auto, tooltip="Bearish Wick Reversal Setup")


// Debugging
//==============================================================================
// plotchar(bar_body, "Bar Body", "", location = location.top, size=size.tiny, color=color.lime)
// plotchar(wick_length, "Wick Length", "", location = location.top, size=size.tiny, color=color.lime)
// plotchar(actual_wick_to_body_ratio, "Wick2Body", "", location = location.top, size=size.tiny, color=color.lime)
// plotchar(bull_close_percentage, "Bull %age", "", location = location.top, size=size.tiny, color=color.green)
// plotchar(bear_close_percentage, "Bear %age", "", location = location.top, size=size.tiny, color=color.red)
// plotchar(close_bullish, "Bull", "", location = location.top, size=size.tiny, color=color.yellow)
// plotchar(close_bearish, "Bear", "", location = location.top, size=size.tiny, color=color.yellow)


